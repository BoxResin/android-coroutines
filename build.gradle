/*
 * Copyright (c) 2016.
 *
 * This file is part of ProcessManager.
 *
 * ProcessManager is free software: you can redistribute it and/or modify it under the terms of version 2.1 of the
 * GNU Lesser General Public License as published by the Free Software Foundation.
 *
 * ProcessManager is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even
 * the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License along with Foobar.  If not,
 * see <http://www.gnu.org/licenses/>.
 */

buildscript {
    ext {
        if (!has('androidPluginVersion')) {
            androidPluginVersion = '3.0.1'
        }
        if (!has('kotlin_version')) {
            kotlin_version = '1.2.0'
        }
    }
    repositories {
        mavenLocal()
        jcenter()
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.15"
    }
}

plugins {
//    id "com.android.library"
    id "com.jfrog.bintray" version "1.8.0"
}

apply plugin: 'com.android.library'

apply plugin: 'maven-publish'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'org.jetbrains.dokka-android'


version = '0.6.0-pre'
group = 'net.devrieze'
description = 'Library to add coroutine support for Android flow'

ext {
    if (!rootProject.hasProperty('androidTarget')) {
        androidTarget = 26
    }
    if (!rootProject.hasProperty('androidCompatVersion')) {
        androidCompatVersion = androidTarget == 25 ? '25.3.1' : (26 ? '26.1.0' : '27.0.2' )
    }
    if (!rootProject.hasProperty('androidBuildToolsVersion')) {
        androidBuildToolsVersion = '27.0.1'
    }
}

repositories {
    mavenLocal()
    jcenter()
    google()
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}

android {
    compileSdkVersion androidTarget
    buildToolsVersion "${androidBuildToolsVersion}"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion androidTarget
        versionName version
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation "com.android.support:support-v4:${androidCompatVersion}"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.esotericsoftware:kryo:4.0.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.19.3"
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.kotlin.srcDirs
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

dokka {
    linkMapping {
        dir='src/main/java'
        url = 'https://github.com/pdvrieze/android-coroutines/tree/master/src/main/java'
        suffix = '#L'
    }
    outputFormat = 'html'
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            artifact bundleRelease
            groupId group
            artifactId 'android-coroutines'

            artifact androidSourcesJar {
                classifier "sources"
            }
        }
    }
}

bintray {
    if (rootProject.hasProperty('bintrayUser')) {
        user = bintrayUser
        key = bintrayApiKey
    }
    publications = ['MyPublication']

    pkg {
        repo = 'maven'
        name = 'android-coroutines'
        userOrg = 'pdvrieze'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/pdvrieze/android-coroutines.git'

        version {
            name = project.version
            desc = 'Initial public release'
            released = new Date()
            vcsTag = "v$version"
        }
    }
}


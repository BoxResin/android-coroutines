/*
 * Copyright (c) 2016.
 *
 * This file is part of ProcessManager.
 *
 * ProcessManager is free software: you can redistribute it and/or modify it under the terms of version 2.1 of the
 * GNU Lesser General Public License as published by the Free Software Foundation.
 *
 * ProcessManager is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even
 * the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License along with Foobar.  If not,
 * see <http://www.gnu.org/licenses/>.
 */

buildscript {
    ext {
        if (!has('androidPluginVersion')) {
            androidPluginVersion = '3.1.1'
        }
        if (!has('kotlin_version')) {
            kotlin_version = '1.2.30'
        }
    }
    repositories {
        jcenter()
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${androidPluginVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.15"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0"
    }
}

apply plugin: "com.jfrog.bintray"

apply plugin: 'com.android.library'

apply plugin: 'maven-publish'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'org.jetbrains.dokka-android'

version = '0.6.3'
group = 'net.devrieze'
description = 'Library to add coroutine support for Android flow'

ext {
    if (!rootProject.hasProperty('androidTarget')) {
        androidTarget = 27
    }
    if (!rootProject.hasProperty('androidCompatVersion')) {
        androidCompatVersion = '27.1.1'
    }
}

repositories {
    mavenLocal()
    jcenter()
    google()
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

android {
    compileSdkVersion androidTarget

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion androidTarget
        versionName version
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation "com.android.support:support-v4:${androidCompatVersion}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.esotericsoftware:kryo:4.0.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
//    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.22.5"
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

androidExtensions {
    experimental = true
}

dokka {
    linkMapping {
        dir='src/main/java'
        url = 'https://github.com/pdvrieze/android-coroutines/tree/master/src/main/java'
        suffix = '#L'
    }
    outputFormat = 'html'
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            artifact bundleRelease
            groupId group
            artifactId 'android-coroutines'

            artifact androidSourcesJar {
                classifier "sources"
            }
        }
    }
}

bintray {
    if (rootProject.hasProperty('bintrayUser')) {
        user = bintrayUser
        key = bintrayApiKey
    }
    publications = ['MyPublication']

    pkg {
        repo = 'maven'
        name = 'android-coroutines'
        userOrg = 'pdvrieze'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/pdvrieze/android-coroutines.git'

        version {
            name = project.version
            desc = 'Update to recent Kotlinx.coroutines. Fix some bugs related to capture of magic context handling'
            released = new Date()
            vcsTag = "v$version"
        }
    }
}

bintrayUpload {
    dependsOn(androidSourcesJar)
}